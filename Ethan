@name Ethan
@inputs Base:entity Seat:entity CamAng:angle W2 S2 Shift2 Crouch2 CamAng2:angle
@outputs CamPos:vector CamDistance Active Fire

#-- front legs bones
@inputs [Thigh_R Thigh_L Leg_R Leg_L Foot_R Foot_L]:entity
#-- core variables
@persist [Time_delta Time_current] [Down Left]:vector [Ang]:angle Inertia:angle Mass CamPos:vector CamDistance Active A3 Editmode Duck
#-- mech variables
@persist [Mech_height Mech_speed Mech_gait_time Mech_gait_transition] [Mech_pos Mech_vel Leg_Pos_Offset]:vector [Align]:angle [Mech_gaits]:table [Mech_gait_last Mech_gait_next Leg_Rotations Leg_Positions]:array BonesVisible StepDistance StepDistanceRunReduceMult Base_Speed Run_Speed_Mult Crouch_Speed_Reduce_Mult Crouch_Run_Speed_Reduce_Mult Speed_Mult StepSound:string
#-- feet variables
@persist [Left_a_real]:angle [Left_ent]:entity [Left_groundz Left_state Left_stepz Left_time] [Left_a_last Left_a_next]:quaternion [Left_p_foot Left_p_last Left_p_move Left_p_next Left_p_real]:vector
@persist [Right_a_real]:angle [Right_ent]:entity [Right_groundz Right_state Right_stepz Right_time] [Right_a_last Right_a_next]:quaternion [Right_p_foot Right_p_last Right_p_move Right_p_next Right_p_real]:vector

#-- ik variables
@persist LengthA LengthB

#-- cam stuff
@persist ThirdPerson [ThirdPersonPos FirstPersonPos]:vector ThirdPersonDistance FirstPersonDistance

@trigger none
#-- animation base by shadowscion

#ifdef httpRequest(string)
@persist [Version Link]:string Block
#endif

if (first()) {
    
    Editmode = 1
    
    rangerHitEntities(!Editmode)
    
    rangerPersist(1)
    rangerFilter(players())
    rangerFilter(Base:children())
    rangerFilter(Base)
    rangerFilter(Base:getConstraints())
    
    Base:setMass(2000)
    
    Mass = Base:mass()
    Inertia = shiftL(ang(Base:inertia()))

    ThirdPersonPos = vec(0,0,5)
    ThirdPersonDistance = 75
    
    FirstPersonPos = vec(0,0,35)
    FirstPersonDistance = 0
    
    #include "ethan_ik_function"
    
    #-- mech variables
    
    Thigh_Length = 16
    Calf_Length = 18.15
    
    Mech_height = 55.6
    Leg_Pos_Offset = vec(0,-5.6,-2)
    
    StepDistance = 185
    StepDistanceRunReduceMult=1.6
    
    Base_Speed = 19
    Run_Speed_Mult = 1.8
    Crouch_Speed_Reduce_Mult = 0.42
    Crouch_Run_Speed_Reduce_Mult = 0.5
    Speed_Mult = 0.6
    
    StepSound = "npc/dog/dog_footstep2.wav"
    
    Leg_Rotations = array(
        #-- idle --
        #right, left
        -5,3,
        #-- crouch --
        #right, left
        -9,4
    )
    
    Leg_Positions = array(
        #-- idle --
        #right, left
        -0.8,0.8,
        #-- crouch --
        #right, left
        -12,12
    )
    
    Mech_gaits = table(
        "walk" = array(
            0.8,35,   #cycle rate,speed
            0.0,0.5, #fl start,length
            0.5,0.5 #fr start,length
        ),
        "run" = array(
            1.2,55,
            0.0,0.65,
            0.5,0.65
        )
    )
    
    LengthA = Thigh_Length
    LengthB = Calf_Length
    
    Mech_gait_last = Mech_gaits["walk",array]
    Mech_gait_next = Mech_gaits["walk",array]

    function number gseq(Start,Length,Time) {
        local Offset = Time - (Start % 1)
        return inrange(Offset,-1,Length)*inrange(Offset % 1,0,Length)*(Offset % 1)/Length
    }

    #-- feet variables
    Left_groundz = 3.75
    Right_groundz = Left_groundz

    Left_stepz = 0.3
    Right_stepz = 0.3

    Left_p_foot = vec(0,abs(Leg_Pos_Offset:y()),0)
    Right_p_foot = vec(0,-abs(Leg_Pos_Offset:y()),0)

    Left_p_last = Left_p_next = Left_p_real = entity():toWorld(Left_p_foot)
    Right_p_last = Right_p_next = Right_p_real = entity():toWorld(Right_p_foot)
    
    BonesVisible = 0

    for (I = 1,3) {
        holoCreate(I)
        holoModel(I,"models/sprops/misc/axis_plane.mdl")
        holoAlpha(I,255*BonesVisible)
    }
    holoParent(3,Base)
    holoPos(3,Base:toWorld(vec(0,0,0)))
    holoAng(3,Base:toWorld(ang(0,0,0)))
    
    Left_ent = holoEntity(1)
    Right_ent = holoEntity(2)

    #-- core variables
    Down = vec(0,0,-1)
    Left = vec(0,1,0)
    
    #right
    
    holoCreate(4,Thigh_R:toWorld(vec(0,0,0)),vec(0),Thigh_R:toWorld(ang(0,0,0)),vec(255),"cube")
    holoPos(4,holoEntity(3):toWorld(Leg_Pos_Offset))
    holoAng(4,holoEntity(3):toWorld(ang(0,0,0)))
    holoParent(4,holoEntity(3))
    
    holoCreate(6,Thigh_R:toWorld(vec(0,0,0)),vec(0),Thigh_R:toWorld(ang(0,0,0)),vec(255),"cube")
    local Children=Thigh_R:children()
    rangerFilter(Children)
    Thigh_R:parentTo(holoEntity(6))
    holoPos(6,holoEntity(4):toWorld(vec(0,0,0)))
    holoAng(6,holoEntity(4):toWorld(ang(0,0,0)))
    holoParent(6,holoEntity(4))
    for(I=1,Children:count()){
        Children[I,entity]:parentTo(Thigh_R)
    }
    
    holoCreate(7,Leg_R:toWorld(vec(0,0,0)),vec(0),Leg_R:toWorld(ang(0,0,0)),vec(255),"cube")
    local Children=Leg_R:children()
    rangerFilter(Children)
    Leg_R:parentTo(holoEntity(7))
    holoPos(7,holoEntity(6):toWorld(vec(0,0,LengthA)))
    holoAng(7,holoEntity(6):toWorld(ang(0,0,0)))
    holoParent(7,holoEntity(6))
    for(I=1,Children:count()){
        Children[I,entity]:parentTo(Leg_R)
    }
    
    
    #left
    
    holoCreate(5,Thigh_L:toWorld(vec(0,0,0)),vec(0),Thigh_L:toWorld(ang(0,0,0)),vec(255),"cube")
    holoPos(5,holoEntity(3):toWorld(Leg_Pos_Offset*vec(1,-1,1)))
    holoAng(5,holoEntity(3):toWorld(ang(0,0,0)))
    holoParent(5,holoEntity(3))
    
    holoCreate(8,Thigh_L:toWorld(vec(0,0,0)),vec(0),Thigh_L:toWorld(ang(0,0,0)),vec(255),"cube")
    local Children=Thigh_L:children()
    rangerFilter(Children)
    Thigh_L:parentTo(holoEntity(8))
    holoPos(8,holoEntity(5):toWorld(vec(0,0,0)))
    holoAng(8,holoEntity(5):toWorld(ang(0,0,0)))
    holoParent(8,holoEntity(5))
    for(I=1,Children:count()){
        Children[I,entity]:parentTo(Thigh_L)
    }
    
    holoCreate(9,Leg_L:toWorld(vec(0,0,0)),vec(0),Leg_L:toWorld(ang(0,0,0)),vec(255),"cube")
    local Children=Leg_L:children()
    rangerFilter(Children)
    Leg_L:parentTo(holoEntity(9))
    holoPos(9,holoEntity(8):toWorld(vec(0,0,LengthA)))
    holoAng(9,holoEntity(8):toWorld(ang(0,0,0)))
    holoParent(9,holoEntity(8))
    for(I=1,Children:count()){
        Children[I,entity]:parentTo(Leg_L)
    }
    
    #feet
    
    #right
    holoCreate(12,Foot_R:toWorld(vec(0,0,0)),vec(0),Foot_R:toWorld(ang(0,0,0)),vec(255),"cube")
    local Children=Foot_R:children()
    rangerFilter(Children)
    Foot_R:parentTo(holoEntity(12))
    holoPos(12,holoEntity(7):toWorld(vec(0,0,LengthB)))
    holoAng(12,holoEntity(7):toWorld(ang(0,0,0)))
    holoParent(12,holoEntity(7))
    for(I=1,Children:count()){
        Children[I,entity]:parentTo(Foot_R)
    }
    
    #left
    holoCreate(13,Foot_L:toWorld(vec(0,0,0)),vec(0),Foot_L:toWorld(ang(0,0,0)),vec(255),"cube")
    local Children=Foot_L:children()
    rangerFilter(Children)
    Foot_L:parentTo(holoEntity(13))
    holoPos(13,holoEntity(9):toWorld(vec(0,0,LengthB)))
    holoAng(13,holoEntity(9):toWorld(ang(0,0,0)))
    holoParent(13,holoEntity(9))
    for(I=1,Children:count()){
        Children[I,entity]:parentTo(Foot_L)
    }

    holoCreate(10)
    holoParent(10,holoEntity(3))
    holoPos(10,holoEntity(4):toWorld(vec(0,0,0)))
    holoAng(10,holoEntity(3):toWorld(ang(0,0,0)))
    holoAlpha(10,0)
    
    holoCreate(11)
    holoParent(11,holoEntity(3))
    holoPos(11,holoEntity(5):toWorld(vec(0,0,0)))
    holoAng(11,holoEntity(3):toWorld(ang(0,0,0)))
    holoAlpha(11,0)
    
    rangerFilter(array(Foot_R,Foot_L,Thigh_R,Thigh_L,Leg_R,Leg_L))
    
    timer("update",500)
    
    #Update check code
    
    #ifdef httpRequest(string)
        Link = "https://github.com/4naki/Ethan/blob/main/Ethan"
        VersionCheckLink = "https://raw.githubusercontent.com/4naki/Ethan/main/Version%20Check"
        Version = "1.2"
        
        runOnHTTP(1)
        if(Link!=""){
        }else{
            Block=1
        }
        if(VersionCheckLink!=""){
            httpRequest(VersionCheckLink)
        }else{
            Block=1
        }
    #else
        print("HTTP Core Not Found!")
    #endif
    
    
}elseif (clk("update")) {
    
    Foot_R:propFreeze(0)
    Foot_L:propFreeze(0)
    
    #-- sim
    timer("update",60)
    
    if(changed(Seat:driver():keyWalk())&Seat:driver():keyWalk()){
        ThirdPerson=!ThirdPerson
    }

    if(changed(ThirdPerson)){
        if(ThirdPerson){
            CamPos=ThirdPersonPos
            CamDistance=ThirdPersonDistance
        }else{
            CamPos=FirstPersonPos
            CamDistance=FirstPersonDistance
        }
    }
    
    Mech_pos = Base:toWorld(vec(0,0,0))
    
    if(Active){
        
        W = Seat:driver():keyForward()
        S = Seat:driver():keyBack()
        A = Seat:driver():keyLeft()
        D = Seat:driver():keyRight()
        
        Space = Seat:driver():keyJump()
        
        Crouch = Seat:driver():keyDuck()
        
        Sprint = Seat:driver():keySprint()

        Fire = Seat:driver():keyAttack1()
        
        CamAng3=CamAng
        
    }else{
        
        W = W2
        S = S2
        A = 0
        D = 0
        
        Crouch = 0
        
        Sprint = Shift2

        Fire = 0
        
        CamAng3=CamAng2
        
    }
    
    if(Active){
        if(changed(Crouch)&Crouch){
            Duck=!Duck
        }
    }else{
        Duck=Crouch2
    }
    
    if(!Editmode){
    
    Dir = Base:vel()
    Mech_ang = Base:toWorld(ang(0,0,0))
    
    Mech_Vel_Y = (Base:right()*Base:velL():y()*0.15)
    
    Mech_Vel_Y2 = Mech_Vel_Y*(1-(2*clamp(A+D,0,1)))
    
    Mech_vel = ((((Base:vel()*(1/(1+(Sprint*StepDistanceRunReduceMult))))*vec(1,1,0))*0.0045)*min(Dir:length(),Mech_speed)*(StepDistance/100)) + Mech_Vel_Y2
    
    Ranger_Right_Front = rangerOffset((Mech_height)*2,rangerOffset(Mech_pos,toWorld(Right_p_foot+(vec(Leg_Positions[1,number]+(Leg_Positions[3,number]*Duck),0,0)*clamp(1-(Dir:length()*0.01),0,1)),Ang,Mech_pos,Mech_ang) + Right_p_move):position(),Down)
    Ranger_Left_Front = rangerOffset((Mech_height)*2,rangerOffset(Mech_pos,toWorld(Left_p_foot+(vec(Leg_Positions[2,number]+(Leg_Positions[4,number]*Duck),0,0)*clamp(1-(Dir:length()*0.01),0,1)),Ang,Mech_pos,Mech_ang) + Left_p_move):position(),Down)
        
    On_Ground = Ranger_Left_Front:hit()&Ranger_Right_Front:hit()
    
    RollBase = clamp(Base:velL():y()*0.05,-15,15)*0.6
    
    Roll = RollBase*(1-(2*clamp(A+D,0,1)))
    
    Pitch = clamp(Base:velL():x()*(0.02+(0.05*Sprint)),-5,15)*0.7
    
    Base:applyAngForce((ang(0, Base:toLocal(ang(0,CamAng3:yaw(),0)):yaw()*55, 0) + 
    Base:toLocal(ang(Pitch, Base:angles():yaw(), Roll))*200 -
    Base:angVel()*20 - ang(Base:angVel():pitch(),0,Base:angVel():roll())*10) * Inertia)
    
    Leg_Distance = Foot_R:pos():distance(Foot_L:pos())
    
    Leg_Distance2 = (clamp((-11.38+Leg_Distance)*0.0022,0,0.5))
    
    A3++
    
    Height_Adj = Mech_height*((0.95/(1+Leg_Distance2+clamp(Duck*0.9,0,1.1)))+(sin(A3*(randint(30,50)/10))*(0.001*sin(A3*(randint(5,15)/10)))))
    
    local Distance = (Ranger_Left_Front:distance()+Ranger_Right_Front:distance())/2
    
    local Movement_Direction = (((Base:forward()*(W-S))+(Base:right()*(D-A)))*vec(1,1,0)):normalized()*On_Ground
    
    if(On_Ground){
        local Stand_Force=vec(0,0,((Height_Adj)*0.75)-Distance)*1.5
        local Damping_Force=(-Base:vel()*vec(0.2,0.2,0.5))*1
        local Move_Force=(((Movement_Direction)*Base_Speed)*(1+(Sprint*(Run_Speed_Mult*(1-(Crouch_Run_Speed_Reduce_Mult*Duck)))))*Speed_Mult)*(1-(Crouch_Speed_Reduce_Mult*Duck))
        local LegsPos=(holoEntity(1):pos()+holoEntity(2):pos())/2
        local Sway=((Base:right()*(sin(A3*(randint(25,70)/10))))+(Base:forward()*(sin(A3*(randint(25,70)/10)))))*0.1
        local Dynamic_Movement=((((((Base:pos()+LegsPos))/2)-Base:pos())*vec(1,1,0))+Sway)*(randint(25,50)/100)
        Base:applyForce((Base:up()*Stand_Force*Mass)+(Damping_Force+Move_Force+Dynamic_Movement)*Mass)
    }
    if(changed(On_Ground)){
        Base:propGravity(!On_Ground)
    }
    
    Ent2Pos=holoEntity(2):pos()
    Ent1Pos=holoEntity(1):pos()
    
    }else{
        On_Ground = 1
        Ent2Pos=Base:toWorld(vec(0,Leg_Pos_Offset:y(),(-Mech_height)+19.5))
        Ent1Pos=Base:toWorld(vec(0,-Leg_Pos_Offset:y(),(-Mech_height)+19.5))
    }
    
    RotationRight = ((Leg_Rotations[1,number]*clamp(1-(Dir:length()*0.01),0,1))+Leg_Rotations[3,number]*Duck)
    RotationLeft = ((Leg_Rotations[2,number]*clamp(1-(Dir:length()*0.01),0,1))+Leg_Rotations[4,number]*Duck)
    
    #right leg
    ik(Base, 10, 4, 6, 7, 12, LengthA, LengthB, holoEntity(10), Ent2Pos, On_Ground, 90, RotationRight*!Editmode,(Right_a_real*ang(1,0,1)))
    
    #left leg
    ik(Base, 11, 5, 8, 9, 13, LengthA, LengthB, holoEntity(11), Ent1Pos, On_Ground, 90, RotationLeft*!Editmode,(Left_a_real*ang(1,0,1)))
    
    if(!Editmode){
    
    Time_delta = curtime() - Time_current
    Time_current = curtime()

    if (Time_delta > 1) {
        exit()
    }

    #-- gait sequencer
    
    if (Mech_gait_transition < 1) {
        
        Mech_gait_transition = min(Mech_gait_transition + (Time_delta*2.5),1)
            
        Mech_gait_time += ((1 - Mech_gait_transition)*Mech_gait_last[1,number] + Mech_gait_transition*Mech_gait_next[1,number])*Time_delta

        Left_time = gseq((1 - Mech_gait_transition)*Mech_gait_last[3,number] + Mech_gait_transition*Mech_gait_next[3,number],(1 - Mech_gait_transition)*Mech_gait_last[4,number] + Mech_gait_transition*Mech_gait_next[4,number],Mech_gait_time)
        Right_time = gseq((1 - Mech_gait_transition)*Mech_gait_last[5,number] + Mech_gait_transition*Mech_gait_next[5,number],(1 - Mech_gait_transition)*Mech_gait_last[6,number] + Mech_gait_transition*Mech_gait_next[6,number],Mech_gait_time)
        Mech_speed = (1 - Mech_gait_transition)*Mech_gait_last[2,number] + Mech_gait_transition*Mech_gait_next[2,number]
    }
    else {
        
        Mech_gait_time += Mech_gait_next[1,number]*Time_delta
        
        Left_time = gseq(Mech_gait_next[3,number],Mech_gait_next[4,number],Mech_gait_time)
        Right_time = gseq(Mech_gait_next[5,number],Mech_gait_next[6,number],Mech_gait_time)
        
        Mech_speed = Mech_gait_next[2,number]
    }
    if (Mech_gait_time > 1) {
        Mech_gait_time--
    }

    #-- stepgen
    if (Left_time) {
        if (!Left_state) {
            soundStop("FL_step")
            Left_a_last = Left_a_next
            Left_p_last = Left_p_next
            Left_p_move = Mech_vel
            Left_state = 1
        }
        local Check = Ranger_Left_Front
        if (Check:hit()) {
            local Normal = Check:hitNormal()
            Left_p_next = Check:position() + Normal*Left_groundz
            local Line = Left_p_next - Left_p_last
            if (Line:length() > Left_groundz) {
                Left_p_real = mix(bezier(Left_p_last,mix(Left_p_next,Left_p_last,0.85) + Line:toAngle():up()*Line:length()*Left_stepz,Left_p_next,Left_time),Left_p_real,Left_time)
                Left_a_next = quat(Normal:cross(Left_p_move):normalized():cross(Normal),Normal)
                Left_a_real = slerp(quat(Left_a_real),slerp(Left_a_last,Left_a_next*qRotation(Left,sin(180*Left_time)*45),Left_time),Left_time):toAngle()

                if (Left_state == 1) {
                    Left_state = 2
                }

                soundVolume("FL_move",0.75 - 0.75*Left_time)
                soundPitch("FL_move",50 - 10*Left_time)
            }
        }
    }
    elseif (Left_state) {
        if (Left_state == 2) {
            if (Left_ent) {
                soundStop("FL_move")
                Left_ent:soundPlay("FL_step",0,StepSound)
            }
        }
        Left_p_real = Left_p_next
        Left_a_real = Left_a_next:toAngle()
        Left_state = 0
    }
    #-- stepgen
    if (Right_time) {
        if (!Right_state) {
            soundStop("FR_step")
            Right_a_last = Right_a_next
            Right_p_last = Right_p_next
            Right_p_move = Mech_vel
            Right_state = 1
        }
        local Check = Ranger_Right_Front
        if (Check:hit()) {
            local Normal = Check:hitNormal()
            Right_p_next = Check:position() + Normal*Right_groundz
            local Line = Right_p_next - Right_p_last
            if (Line:length() > Right_groundz) {
                Right_p_real = mix(bezier(Right_p_last,mix(Right_p_next,Right_p_last,0.85) + Line:toAngle():up()*Line:length()*Right_stepz,Right_p_next,Right_time),Right_p_real,Right_time)
                Right_a_next = quat(Normal:cross(Right_p_move):normalized():cross(Normal),Normal)
                Right_a_real = slerp(quat(Right_a_real),slerp(Right_a_last,Right_a_next*qRotation(Left,sin(180*Right_time)*45),Right_time),Right_time):toAngle()

                if (Right_state == 1) {
                    Right_state = 2
                }

                soundVolume("FR_move",0.75 - 0.75*Right_time)
                soundPitch("FR_move",50 - 10*Right_time)
            }
        }
    }
    elseif (Right_state) {
        if (Right_state == 2) {
            if (Right_ent) {
                soundStop("FR_move")
                Right_ent:soundPlay("FR_step",0,StepSound)
            }
        }
        Right_p_real = Right_p_next
        Right_a_real = Right_a_next:toAngle()
        Right_state = 0
    }
    #-- mech

    holoPos(1,Left_p_real)
    holoPos(2,Right_p_real)

    holoAng(1,Left_a_real)
    holoAng(2,Right_a_real)

    local Dist = Dir:length() > 300
    if (changed(Sprint)) {
        Mech_gait_transition = 0
        Mech_gait_last = Mech_gait_next
        if (Sprint) {
            Mech_gait_next = Mech_gaits["run",array]
        } else { 
            Mech_gait_next = Mech_gaits["walk",array]
        }
    }
    
    }
}

if(changed(Seat:driver())){
    if(Seat:driver()){
        Active = 1
    }else{
        Active = 0
    }
}

if(dupefinished()){
    reset()
}

#ifdef httpRequest(string)
    if(httpClk()){
        if(httpSuccess()&!Block){
            ReturnData=httpUrlDecode(httpData())
            
            print("["+entity():getName()+"]")
            
            if(Version:toNumber()!=ReturnData:toNumber()){
                print("Update available! Running version: "+Version)
                print("Latest version: "+ReturnData)
                print("Link: "+Link)
            }else{
                print("Up to date! Running version: "+Version)
            }
        }
    }
#endif
